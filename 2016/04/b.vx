#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

def checksum (letters)
{
	var histogram = x"00" * 26
	
	for c in letters do
	{
		let i = u8 c - u8 'a'
		
		histogram[ i ] = byte (u8 histogram[ i ] + 1)
	}
	
	var result = ""
	
	for i in 1 .. 5 do
	{
		let max = histogram per Math.max
		
		let j = (begin histogram).find(max).past.size
		
		histogram[ j ] = '\0'
		
		result = result byte (j + u8 'a')
	}
	
	return result
}

let abc = str ('a' .. 'z')

let abc_2 = abc * 2

def decrypt (name, n)
{
	let dst = substr( abc_2, n % 26, 26 )
	
	return translated( name, abc, dst )
}

for room in data do
{
	let len = room.length
	
	let tag = room[ len - 6 -> len - 1 ]
	
	let name-id = room[ 0 -> len - 7 ]
	
	let words = [name-id / '-']
	
	let n = words.length - 1
	
	let id = int words[ n ]
	
	let letters = str words[ 0 -> n ]
	
	if checksum letters == tag then
	{
		let name = join( " ", *words[ 0 -> n ] )
		
		if decrypt( name, id ) == "northpole object storage" then
		{
			print id
			break
		}
	}
}
