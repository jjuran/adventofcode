#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]

var data = (load bindir "/7.txt")

translate( &data, "[]", "  " )

const addresses = data.lines()

def is_aba
{
	return _[ 0 ] == _[ 2 ] and _[ 0 ] != _[ 1 ]
}

def get_ABAs
{
	const segment = _
	
	var result = []
	
	const len = segment.length
	
	for i in 0 .. len - 3 do
	{
		const seg = segment[ i -> i + 3 ]
		
		if is_aba seg then
		{
			result <-- seg
		}
	}
	
	return result
}

var count = 0

for addr in addresses do
{
	const segments = [addr / ' ']
	
	const n = half segments.length
	
	const even = 0 .. n map {_ * 2}
	const odd  = 0 -> n map {_ * 2 + 1}
	
	var good = false
	
	var BABs = str^[]
	
	for s in segments[ odd ] do
	{
		for bab in get_ABAs s do
		{
			BABs[ bab ] = true
		}
	}
	
	if BABs.length == 0 then {continue}
	
	for s2 in segments[ even ] do
	{
		for aba in get_ABAs s2 do
		{
			const bab = aba[[ 1, 0, 1 ]]
			
			if bab in BABs then
			{
				good = true
				break
			}
		}
		
		if good then {break}
	}
	
	if good then {++count}
}

print count
