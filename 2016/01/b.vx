#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()[ 0 ]

let directions = [" " data / ','] map { v[1]: int substr( v, 2 ) }

var x = 0
var y = 0

var dx = 0
var dy = 1

var visited = str^[]

let turn = byte^
[
	'L': { dx, dy = -dy, dx },
	'R': { dx, dy = dy, -dx },
]

def go (n)
{
	x += n * dx
	y += n * dy
}

var done = false

for dir in directions do
{
	turn[ dir.key ]()
	
	for i in 1 .. dir.value do
	{
		go 1
		
		let k = join( ",", x, y )
		
		if k in visited then
		{
			done = true
			break
		}
		
		visited[ k ] = true
	}
	
	if done then {break}
}

let n = abs x + abs y

put n
