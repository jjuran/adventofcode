#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const N = 400000

var data = (load input_path).lines()[ 0 ]  # e.g. ".^^.^.^^^^"

translate( &data, ".^", "\x00\xFF" )

var odds = packed mince data             # e.g. ".^^^^"  i.e. "01111"
var even = packed mince substr(data, 1)  # e.g. "^..^^"  i.e. "10011"

assert even.size == odds.size

const n = even.size

var total = (data ver ...).length

for i in 1 -> N do
{
	if i mod 2^9 == 0 then
	{
		display "" i
	}
	
	odds, even = x"00" even[ 0 -> n - 1 ] ~ even, odds ~ odds[ 1 -> n ] x"00"
	
	total += (odds even ver ...).length
}

total = (2 * n * N - total)

display ""

print total

end.

def generate_odd_from_even (code)
{
	# E.g. "01001" ~ "10011" --> "11010"
	#        ^^^^     ^^^^^       vvvvv
	#        2468     24680       13579
	
	return x"00" code[ 0 -> n - 1 ] ~ code
}

def generate_even_from_odd (code)
{
	# E.g. "01111" ~ "11110" --> "10001"
	#       ^^^^^     ^^^^        vvvvv
	#       13579     3579        24680
	
	return code ~ code[ 1 -> n ] x"00"
}
