#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let digit = '0' .. '9'
let value = digit+ => int

let IP_range =
[
	value, "-",
	value, "\n",
]

let schema = (IP_range => {[_]})+

var p = begin data

let IP_ranges = Iter.sorted [p += schema] via { v[0] }

var lower = 0
var upper = 0
var total = 0

for r in IP_ranges do
{
	if r[ 0 ] > lower then
	{
		total += r[ 0 ] - lower
	}
	
	lower = Math.max( lower, r[ 1 ] + 1 )
	upper = Math.max( lower, upper )
}

if upper < 2^32 then
{
	total += 2^32 - upper
}

put total
