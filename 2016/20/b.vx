#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = load input_path

const digit = '0' .. '9'
const value = digit+ => int

const IP_range =
[
	value, "-",
	value, "\n",
]

const schema = (IP_range => {[_]})+

var p = begin data

const IP_ranges = Iter.sorted [p += schema] via { v[0] }

var lower = 0
var upper = 0
var total = 0

for r in IP_ranges do
{
	if r[ 0 ] > lower then
	{
		total += r[ 0 ] - lower
	}
	
	lower = Math.max( lower, r[ 1 ] + 1 )
	upper = Math.max( lower, upper )
}

if upper < 2^32 then
{
	total += 2^32 - upper
}

print total
