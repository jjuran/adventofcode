#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = translated (load input_path, "[]", "  ")

let addresses = data.lines()

def is_abba (s)
{
	return s[ 0 ] == s[ 3 ] and s[ 1 ] == s[ 2 ] and s[ 0 ] != s[ 1 ]
}

def has_abba (segment)
{
	let len = segment.length
	
	for i in 0 .. len - 4 do
	{
		if is_abba segment[ i -> i + 4 ] then
		{
			return true
		}
	}
	
	return false
}

var count = 0

for addr in addresses do
{
	let segments = [addr / ' ']
	
	let n = half segments.length
	
	let even = 0 .. n map {v * 2}
	let odd  = 0 -> n map {v * 2 + 1}
	
	var good = false
	var bad  = false
	
	for s in segments[ odd ] do
	{
		if has_abba s then {bad = true; break}
	}
	
	if bad then {continue}
	
	for s2 in segments[ even ] do
	{
		if has_abba s2 then {good = true; break}
	}
	
	if good then {++count}
}

put count
