#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vsl" }

let display = (import "status") { OUT <== _ }

let data = "ugkcyxxp"

let n = 5

var suffix = 0

var password = " " * 8

while ' ' in password do
{
	if (hex md5 data ++suffix)[ 0 -> n ] == "0" * n then
	{
		let output = hex md5 data suffix
		
		let o6 = output[ 5 ]
		
		if o6 <= '7' then
		{
			let pos = u8 o6 - 0x30
			
			if password[ pos ] == ' ' then
			{
				password[ pos ] = output[ 6 ]
			}
		}
		
		display password "  " output "  " data suffix
	}
}

display ""

put password
