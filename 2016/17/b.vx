#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const data = "vkjiggvb"

def successors (path)
{
	const up   = Iter.popcount translated (path, "DLR", "\0" * 3)
	const down = Iter.popcount translated (path, "ULR", "\0" * 3)
	
	const left  = Iter.popcount translated (path, "UDR", "\0" * 3)
	const right = Iter.popcount translated (path, "UDL", "\0" * 3)
	
	const dx = down - up
	const dy = right - left
	
	const hash = hex md5 data path
	
	const steps =
	[
		"U" (*) (dx > 0 and hash[ 0 ] > 'a'),
		"D" (*) (dx < 3 and hash[ 1 ] > 'a'),
		"L" (*) (dy > 0 and hash[ 2 ] > 'a'),
		"R" (*) (dy < 3 and hash[ 3 ] > 'a'),
	]
	
	if dx + dy == 6 then
	{
		return ()  # We're at the exit
	}
	
	return steps map { path v }
}

def longest_path_length
{
	var distance = 0
	var longest  = 0
	
	var frontier = [ "" ]
	
	while frontier do
	{
		display "" ++distance
		
		var new_frontier = []
		
		for path in frontier do
		{
			const paths = successors path
			
			if paths then
			{
				for path in paths do
				{
					new_frontier <-- path
				}
			}
			else if paths isa () then
			{
				longest = distance
			}
		}
		
		frontier <- new_frontier
	}
	
	return longest - 1
}

const longest = longest_path_length()

display ""

print longest
