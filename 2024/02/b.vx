#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let lines = (load input_path).lines()

def safe (report)
{
	let gaps = report gap { a - b }
	
	let unsafe = gaps pop { not v in 1 .. 3 } and gaps pop { not v in -3 .. -1 }
	
	return not unsafe
}

def mostly_safe (report)
{
	if safe report then
	{
		return true
	}
	
	let n = report.length
	
	for i in 0 -> n do
	{
		let dampened_report = [ *report[ 0 -> i ], *report[ i + 1 -> n ] ]
		
		if safe dampened_report then
		{
			return true
		}
	}
	
	return false
}

let count = lines map { [v / ' '] map int } pop mostly_safe

put count
