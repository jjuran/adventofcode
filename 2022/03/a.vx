#!/usr/bin/env minivx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

def priority (ascii)
{
	if ascii in 'a' .. 'z' then
	{
		return u8 ascii - u8 'a' + 1
	}
	
	if ascii in 'A' .. 'Z' then
	{
		return u8 ascii - u8 'A' + 27
	}
	
	# not reached
}

def inventory (items)
{
	var bitset = x"00" * 128
	
	for c in items do
	{
		bitset[ u8 c ] = 'x'
	}
	
	return bitset
}

def search (rucksack)
{
	let a, let b = rucksack / 2
	
	let both = inventory a & inventory b
	
	let p = (begin both).find 'x'
	
	return byte p.past.size
}

print (data map search map priority per Math.sum)
