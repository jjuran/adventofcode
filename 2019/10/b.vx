#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path
let pipe = (load bindir "/10-pipe.txt")

let lines = data.lines()

var grid = str lines

let height = lines.length
let width = grid.length / height

let (X, Y) = eval pipe

def set_cell( x, y, v )
{
	grid[ x + y * width ] = v
}

set_cell( X, Y, '*' )  # laser cannon

def contains( x, y )
{
	return x in 0 -> width and y in 0 -> height
}

def angular_ordering (coordinates)
{
	let (x, y) = *coordinates
	
	if x == 0 or y == 0 then
	{
		return (+(y == 0) + 2 * +(y - x > 0)) * 10^6
	}
	
	let quadrant = +(x > 0 == y > 0) + 2 * +(x < 0)
	
	let slope = if x > 0 == y > 0
		then {  1000 * y div x }
		else { -1000 * x div y }
	
	return quadrant * 10^6 + slope
}

def mark_obstructed_along_ray( gridref, x, y, dx, dy )
{
	var obstructed = false
	
	while contains( (x += dx), (y += dy) ) do
	{
		let index = x + y * width
		
		if (*gridref)[ index ] == 'X' then
		{
			# This spot was already marked, presumably by an earlier dx/dy pass
			# that's a factor of this one.
			break
		}
		
		if obstructed then
		{
			(*gridref)[ index ] = 'X'
		}
		else if grid[ index ] == '#' then
		{
			obstructed = true
		}
	}
}

def laser_rotation_sweep_targets
{
	for i in 0 -> width do
	{
		for j in +(i == 0) -> height do
		{
			mark_obstructed_along_ray( &grid, X, Y,  i,  j )
			mark_obstructed_along_ray( &grid, X, Y,  i, -j )
			mark_obstructed_along_ray( &grid, X, Y, -i,  j )
			mark_obstructed_along_ray( &grid, X, Y, -i, -j )
		}
	}
	
	var targets = []
	
	for x in 0 -> width do
	{
		for y in 0 -> height do
		{
			if grid[ x + y * width ] == '#' then
			{
				targets <-- [x - X, y - Y]
			}
		}
	}
	
	return Iter.sorted targets via angular_ordering
}

let targets = laser_rotation_sweep_targets()

def abs_from_rel (x, y)
{
	return (X + x, Y + y)
}

def mux (x, y)
{
	return x * 100 + y
}

let the_200th = targets[ 199 ]

put mux abs_from_rel *the_200th
