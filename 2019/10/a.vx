#!/usr/bin/env minivx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let lines = data.lines()

let grid = str lines

let height = lines.length
let width = grid.length / height

def mark_obstructed_along_ray( gridref, x, y, dx, dy )
{
	var obstructed = false
	
	while (x += dx) in 0 -> width and (y += dy) in 0 -> height do
	{
		let index = x + y * width
		
		if obstructed then
		{
			(*gridref)[ index ] = 'X'
		}
		else if grid[ index ] == '#' then
		{
			obstructed = true
		}
	}
}

def count_asteroids( grid, index )
{
	if grid[ index ] != '#' then
	{
		return 0
	}
	
	let x = index mod width
	let y = index div width
	
	mark_obstructed_along_ray( &grid, x, y,  1,  0 )
	mark_obstructed_along_ray( &grid, x, y, -1,  0 )
	
	mark_obstructed_along_ray( &grid, x, y,  0,  1 )
	mark_obstructed_along_ray( &grid, x, y,  0, -1 )
	
	mark_obstructed_along_ray( &grid, x, y,  1,  1 )
	mark_obstructed_along_ray( &grid, x, y,  1, -1 )
	mark_obstructed_along_ray( &grid, x, y, -1,  1 )
	mark_obstructed_along_ray( &grid, x, y, -1, -1 )
	
	for i in 1 -> width do
	{
		for j in i + 1 -> height do
		{
			if i != 1 and str (j/i) != j "/" i then
			{
				continue
			}
			
			mark_obstructed_along_ray( &grid, x, y,  i,  j )
			mark_obstructed_along_ray( &grid, x, y,  i, -j )
			mark_obstructed_along_ray( &grid, x, y, -i,  j )
			mark_obstructed_along_ray( &grid, x, y, -i, -j )
			
			mark_obstructed_along_ray( &grid, x, y,  j,  i )
			mark_obstructed_along_ray( &grid, x, y,  j, -i )
			mark_obstructed_along_ray( &grid, x, y, -j,  i )
			mark_obstructed_along_ray( &grid, x, y, -j, -i )
		}
	}
	
	return (grid pop {v == '#'}) - 1
}

var max_count = 0
var max_index = "-"

display "Max: [" max_index "] " max_count

for i in 0 -> grid.length do
{
	let count = count_asteroids( grid, i )
	
	if count > max_count then
	{
		max_count = count
		max_index = i
	}
	
	display "Max: [" max_index "] " max_count " | [" i "] " count
}

display ""

let x = max_index mod width
let y = max_index div width

put max_count

rewrite bindir "/10-pipe.txt" <== rep (x, y), "\n"
