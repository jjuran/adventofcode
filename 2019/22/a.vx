#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let lines = data.lines()

let N = 10007

let card = 2019

var position = card

def reverse
{
	position = N - 1 - position
}

def cut (n)
{
	if n < 0 then
	{
		n = N + n
	}
	
	position = (position - n) mod N
}

def radix (inc)
{
	position = position * inc % N
}

for row in lines do
{
	let words = [row / ' ']
	
	if words[ 0 ] == "cut" then
	{
		let n = int words[ 1 ]
		
		cut n
	}
	else if words[ 1 ] == "with" then
	{
		let increment = int words[ 3 ]
		
		radix increment
	}
	else
	{
		reverse()
	}
}

put position
