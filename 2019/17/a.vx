#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }
let intcode = import "intcode"

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()[0]
let core = [data / ','] map int

let machine = intcode.machine core

var text = ""
var scan = "Visual scan: "

display "Booting ASCII..."

for output in receiver machine.run do
{
	text = text byte output
	
	if output == u8 '\n' then
	{
		scan .= "."
	}
	
	display scan
}

display "Done."

let lines = text.lines() ver ...

let width  = lines[ 0 ].length
let height = lines.length

let image = str lines

def is_scaffold (x, y)
{
	return image[ x + y * width ] == '#'
}

let moves =
[
	[  0, -1 ],  # N
	[  0,  1 ],  # S
	[ -1,  0 ],  # W
	[  1,  0 ],  # E
]

def is_intersection (i, j)
{
	let neighbors = moves map { [v[0] + i, v[1] + j] }
	
	return is_scaffold( i, j ) and neighbors pop {is_scaffold *v} > 2
}

var sum = 0

for i in 1 -> width - 1 do
{
	for j in 1 -> height - 1 do
	{
		if is_intersection( i, j ) then
		{
			sum += i * j
		}
	}
}

display ""

put sum
