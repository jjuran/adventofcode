#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const intcode = import "intcode"

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path).lines()[0]
const core = [data / ','] map int

const machine = intcode.machine core

const Tile_empty  = 0
const Tile_wall   = 1
const Tile_block  = 2
const Tile_paddle = 3
const Tile_ball   = 4

var height = 1

def thrice (f)
{
	if (const a = f()) isa () then {return ()}
	
	const b = f()
	const c = f()
	
	return a, b, c
}

var score = ()

const ESC = "\x1b"
const CSI = ESC "["

const CLEAN = CSI "H" CSI "J"  # home, clear to end

const RESET = CSI "0m"
const RED   = CSI "31m"
const GREEN = CSI "32m"
const GREY  = CSI "37m"
const AMBER = CSI "93m"
const WHITE = CSI "97m"

def GOTO (x, y)
{
	return CSI (y + 1) ";" (x + 1) "H"
}

const sprites = " X#_*"

var ball_x
var paddle_x
var max_wall_x = 0

var score_box = ""

def update_score
{
	const score_offset = (max_wall_x + 1) div 2 - "Score: ".length
	const score_indent = " " * score_offset
	
	score_box = score_indent WHITE "Score: " AMBER score RESET
}

def joystick
{
	return ball_x <=> paddle_x
}

machine.poke( 0, 2 )

machine.connect-input joystick

OUT <== CLEAN

while var tuple = thrice machine.run do
{
	const (x, y, t) = tuple
	
	if x < 0 then
	{
		score = t
		
		update_score()
		
		OUT <== GOTO(4, 1) score_box GOTO(0, height + 3)
	}
	else
	{
		if y >= height then
		{
			height = y + 1
		}
		
		var color = ""
		
		if t == Tile_ball then
		{
			ball_x = x
			
			color = AMBER
		}
		else if t == Tile_paddle then
		{
			paddle_x = x
			
			color = WHITE
		}
		else if t == Tile_block then
		{
			color = [GREEN, RED][ y % 2 ]
		}
		else if t == Tile_wall then
		{
			max_wall_x = Math.max( x, max_wall_x )
			
			color = GREY
		}
		
		OUT <== GOTO(x + 4, y + 3) color sprites[ t ] RESET GOTO(0, height + 3)
	}
}
