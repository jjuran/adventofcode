#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }
let intcode = import "intcode"

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()[0]
let core = [data / ','] map int

let machine = intcode.machine core

let Tile_empty  = 0
let Tile_wall   = 1
let Tile_block  = 2
let Tile_paddle = 3
let Tile_ball   = 4

def thrice (f)
{
	if (let a = f()) isa () then {return ()}
	
	let b = f()
	let c = f()
	
	return a, b, c
}

var ball_x
var paddle_x

def joystick
{
	return ball_x <=> paddle_x
}

machine.poke( 0, 2 )

machine.connect-input joystick

var n_blocks = 0

var score = 0

while var tuple = thrice machine.run do
{
	let (x, y, t) = tuple
	
	if x < 0 then
	{
		score = t
		
		display "Score: " score
	}
	else
	{
		if t == Tile_ball then
		{
			ball_x = x
		}
		else if t == Tile_paddle then
		{
			paddle_x = x
		}
		else if t == Tile_block then
		{
			++n_blocks
			
			display "" n_blocks
		}
	}
}

display ""

put score
