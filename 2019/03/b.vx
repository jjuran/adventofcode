#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path).lines()

const one, const two = *(data map { [v / ','] })

def segments (path)
{
	var x = 0
	var y = 0
	var n = 0
	
	var horizontal = []
	var vertical   = []
	
	for move in path do
	{
		const dir = move[ 0 ]
		
		var inc = int substr( move, 1 )
		
		const segment
		
		if dir == 'L' then
		{
			const x0 = x
			x -= inc
			
			horizontal <-- [ -n, x, y, x0 ]
		}
		
		if dir == 'R' then
		{
			const x0 = x
			x += inc
			
			horizontal <-- [ n, x0, y, x ]
		}
		
		if dir == 'D' then
		{
			const y0 = y
			y -= inc
			
			vertical <-- [ -n, x, y, y0 ]
		}
		
		if dir == 'U' then
		{
			const y0 = y
			y += inc
			
			vertical <-- [ n, x, y0, y ]
		}
		
		n += inc
	}
	
	return horizontal, vertical
}

def cross (h, v)
{
	var n, const x0, const y, const x1 = *h
	var m, const x, const y0, const y1 = *v
	
	assert x0 <= x1
	assert y0 <= y1
	
	if x in x0 .. x1 and y in y0 .. y1 then
	{
		if n < 0 then
		{
			n = x1 - x - n
		}
		else
		{
			n += x - x0
		}
		
		if m < 0 then
		{
			m = y1 - y - m
		}
		else
		{
			m += y - y0
		}
		
		return n + m
	}
	
	return 0
}

def crossings( hs, vs )
{
	var result = []
	
	for h in hs do
	{
		for v in vs do
		{
			if const n = cross( h, v ) then
			{
				result <-- n
			}
		}
	}
	
	return result
}

const ah, const av = segments one
const bh, const bv = segments two

const intersects =
[
	*crossings( ah, bv ),
	*crossings( bh, av ),
]

const minimum = intersects per Math.min

print minimum
