#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }
let intcode = import "intcode"

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()[0]
let core = [data / ','] map int

var input_queue

def input
{
	return *input_queue++
}

def query (x, y)
{
	let machine = intcode.machine core
	
	input_queue = begin [ x, y ]
	
	return machine.run input
}

let ship_size = 100

var edge = 10

var grid = " " * edge^2

def embiggen (new_edge)
{
	var new_grid = " " * new_edge^2
	
	for row in 0 -> edge do
	{
		for col in 0 -> edge do
		{
			new_grid[ col + row * new_edge ] = grid[ col + row * edge ]
		}
	}
	
	edge = new_edge
	grid = new_grid
}

var min_x = 0

let tiles = ".#"

def update (x, y)
{
	let r = query _
	
	display "Minimum X: " min_x "  Current: " x "," y
	
	grid[ x + y * edge ] = tiles[ r ]
	
	return r
}

def fill_bottom (limit)
{
	for x in min_x -> limit do
	{
		update( x, limit - 1 )
	}
}

def bottom_row
{
	return substr( grid, grid.length - edge )
}

def right_col
{
	return str ([grid / edge] map { v[edge - 1] })
}

def get_row (y)
{
	return substr( grid, y * edge, edge )
}

def left_beam_edge (row)
{
	return (begin row).find('#').past.length
}

def right_beam_edge (row)
{
	return (begin -row).find('#').rest.length
}

def check_ship_beam
{
	let top    = get_row (edge - ship_size)
	let bottom = bottom_row()
	
	let bottom_left = left_beam_edge bottom
	let top_right   = right_beam_edge top
	
	min_x = left_beam_edge top
	
	return top_right - bottom_left
}

fill_bottom edge

while 1 do
{
	let bottom = bottom_row()
	let right = right_col()
	
	let width  = bottom pop {v == '#'}
	
	let maximum = width
	
	if maximum < ship_size then
	{
		if width then
		{
			min_x = (begin bottom).find('#').past.length
		}
		
		let new_edge = edge * ship_size div (maximum or 2)
		
		if new_edge > edge then
		{
			embiggen new_edge
			
			fill_bottom (new_edge - ship_size + 1)
			fill_bottom new_edge
			
			continue
		}
	}
	
	while true do
	{
		let width = check_ship_beam()
		
		assert width <= ship_size
		
		if width == ship_size then
		{
			break
		}
		
		let new_edge = edge + (ship_size - width)
		
		embiggen new_edge
		
		fill_bottom (new_edge - ship_size + 1)
		fill_bottom new_edge
	}
	
	break
}

let X = left_beam_edge bottom_row()
let Y = edge - ship_size

display ""

put (X * 10000 + Y)
