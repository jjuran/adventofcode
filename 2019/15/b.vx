#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }
let intcode = import "intcode"

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()[0]
let core = [data / ','] map int

let machine = intcode.machine core

let Space = ' '
let Droid = 'D'
let Wall  = '#'
let Floor = '.'
let Prune = 'x'
let O2sys = '@'
let O2air = 'O'

var floorplan = str^[]
var distances = str^[]

distances[ "0,0" ] = 0

var X = 0
var Y = 0

let moves =
[
	null,
	[  0, -1 ],  # N
	[  0,  1 ],  # S
	[ -1,  0 ],  # W
	[  1,  0 ],  # E
]

def forward (dir)
{
	let m = moves[ dir ]
	
	return X + m[ 0 ], Y + m[ 1 ]
}

def sample (x, y)
{
	let key = x "," y
	
	return if key in floorplan then { floorplan[ key ] } else { Space }
}

def is_known (x, y)
{
	return x "," y in floorplan
}

def is_dead_end (x, y)
{
	return 1 .. 4 pop { sample forward v in Wall Prune "" } == 3
}

def uncharted
{
	return 1 .. 4 ver { not is_known forward v }
}

def open
{
	return 1 .. 4 ver { sample forward v == Floor }
}

var next_pos

def next_dir
{
	return head *uncharted() or head *open()
}

var dir

def input
{
	next_pos = forward dir
	
	return dir
}

let tokens = Wall Floor O2sys ""

def gridify
{
	let keys = floorplan.keys
	
	let xkeys = keys map {int [v / ','][0]}
	let ykeys = keys map {int [v / ','][1]}
	
	let min_x = Iter.min xkeys
	let max_x = Iter.max xkeys
	let min_y = Iter.min ykeys
	let max_y = Iter.max ykeys
	
	let width  = max_x + 1 - min_x
	let height = max_y + 1 - min_y
	
	let npx = width * height
	
	var grid = " " * npx
	
	for kv in floorplan do
	{
		let (x, y) = *([kv.key / ','] map int)
		
		grid[ (x - min_x) + (y - min_y) * width ] = kv.value
	}
	
	grid[ (X - min_x) + (Y - min_y) * width ] = Droid
	
	return grid, height
}

var n_steps = 0

while true do
{
	dir = next_dir()
	
	let output = machine.run input
	
	let key = ",".join next_pos
	
	let c = tokens[ output ]
	
	floorplan[ key ] = c
	
	if output != 0 then
	{
		display "Steps: " ++n_steps
		
		if not key in distances then
		{
			distances[ key ] = distances[ X "," Y ] + 1
		}
		
		if is_dead_end( X, Y ) then
		{
			floorplan[ X "," Y ] = Prune
		}
		
		X, Y = next_pos
		
		if output == 2 then
		{
			break
		}
	}
}

let oxypos = X, Y

display "Oxygen system at " rep oxypos

while dir = next_dir() do
{
	let output = machine.run input
	
	let key = ",".join next_pos
	
	let c = tokens[ output ]
	
	floorplan[ key ] = c
	
	if output != 0 then
	{
		if not key in distances then
		{
			distances[ key ] = distances[ X "," Y ] + 1
		}
		
		if is_dead_end( X, Y ) then
		{
			floorplan[ X "," Y ] = Prune
		}
		
		X, Y = next_pos
	}
}

display "Pumping oxygen..."

floorplan[ ",".join oxypos ] = O2sys

var oxymap, let height = gridify()

let keys = floorplan.keys

let xkeys = keys map {int [v / ','][0]}
let ykeys = keys map {int [v / ','][1]}

let min_x = Iter.min xkeys
let max_x = Iter.max xkeys
let min_y = Iter.min ykeys
let max_y = Iter.max ykeys

let width  = max_x + 1 - min_x
#let height = max_y + 1 - min_y

var timer = 0

var oxyfills = [ [oxypos] ]

while true do
{
	var newfills = []
	
	for oxy in oxyfills do
	{
		for m in moves[ 1 .. 4 ] do
		{
			let x = oxy[0] + m[0]
			let y = oxy[1] + m[1]
			
			let index = x - min_x + (y - min_y) * width
			
			if oxymap[ index ] in Prune Floor Droid "" then
			{
				newfills <-- [x, y]
				
				oxymap[ index ] = O2air
			}
		}
	}
	
	if not newfills then
	{
		break
	}
	
	++timer
	
	oxyfills = newfills
}

display ""

put timer
