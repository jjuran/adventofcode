#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let display = (import "status") { OUT <== _ }
let intcode = import "intcode"

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()[0]
let core = [data / ','] map int

let machine = intcode.machine core

let Space = ' '
let Wall  = '#'
let Floor = '.'
let Prune = 'x'
let O2sys = '!'

var floorplan = str^[]
var distances = str^[]

distances[ "0,0" ] = 0

var X = 0
var Y = 0

let moves =
[
	null,
	[  0, -1 ],  # N
	[  0,  1 ],  # S
	[ -1,  0 ],  # W
	[  1,  0 ],  # E
]

def forward (dir)
{
	let m = moves[ dir ]
	
	return X + m[ 0 ], Y + m[ 1 ]
}

def sample (x, y)
{
	let key = x "," y
	
	return if key in floorplan then { floorplan[ key ] } else { Space }
}

def is_known (x, y)
{
	return x "," y in floorplan
}

def is_dead_end (x, y)
{
	return 1 .. 4 pop { sample forward v in Wall Prune "" } == 3
}

def uncharted
{
	return 1 .. 4 ver { not is_known forward v }
}

def open
{
	return 1 .. 4 ver { sample forward v == Floor }
}

var next_pos

def input
{
	let dir = head *uncharted() or head *open()
	
	next_pos = forward dir
	
	return dir
}

let tokens = Wall Floor O2sys ""

var n_steps = 0

for output in receiver (machine.run % input) do
{
	if output == 2 then
	{
		break
	}
	
	let key = ",".join next_pos
	
	let c = tokens[ output ]
	
	floorplan[ key ] = c
	
	if output != 0 then
	{
		display "Steps: " ++n_steps
		
		if not key in distances then
		{
			distances[ key ] = distances[ X "," Y ] + 1
		}
		
		if is_dead_end( X, Y ) then
		{
			floorplan[ X "," Y ] = Prune
		}
		
		X, Y = next_pos
	}
}

display ""

put (distances[ X "," Y ] + 1)
