#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const intcode = import "intcode"

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path).lines()[0]
const core = [data / ','] map int

const machine = intcode.machine core

var painted_panels = str^[]

var x = 0
var y = 0

var dir = 0  # up/right/down/left

const movements =
[
	[0, -1],
	[1, 0],
	[0, 1],
	[-1, 0],
]

def sample
{
	const key = x "," y
	
	if key in painted_panels then
	{
		return painted_panels[ key ]
	}
	
	return 0
}

while (+) (var c = machine.run sample) do
{
	const d = machine.run()
	
	# paint a cell
	painted_panels[ x "," y ] = c
	
	# turn left or right
	dir = (dir + d * 2 - 1) mod 4
	
	# move
	const m = movements[ dir ]
	
	x += m[ 0 ]
	y += m[ 1 ]
}

print painted_panels.length
