#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let display = (import "status") { OUT <== _ }

let sieve-of-eratosthenes = import "sieve-of-eratosthenes"

let lcm = (import "lcm").with sieve-of-eratosthenes

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

def parse_line (s)
{
	return [" " s[ 1 -> s.length - 1 ] / ','] map { int [substr(v, 1) / '='][1] }
}

def dimension (positions)
{
	let n = positions.length
	
	var velocities (int[]) = [0 (*) n]
	
	def state
	{
		return rep (0 -> n map { positions[ v ], velocities[ v ] })
	}
	
	let beginning = state()
	
	def step
	{
		# apply gravity
		
		for i in 1 -> n do
		{
			let a = positions[ i ]
			
			for j in 0 -> i do
			{
				let b = positions[ j ]
				
				let pull = b <=> a
				
				velocities[ i ] += pull
				velocities[ j ] -= pull
			}
		}
		
		# apply velocity
		
		for j in 0 -> n do
		{
			positions[ j ] += velocities[ j ]
		}
		
		return state() != beginning
	}
	
	return record( :step )
}

let positions = data map parse_line

let axes = "XYZ"

def find_period (i)
{
	let axis = axes[ i ] ": "
	
	let d = dimension (positions map { v[ i ] })
	
	var steps = 0
	
	do
	{
		++steps
		
		if u8 steps == 0 then
		{
			display axis steps
		}
	}
	while d.step()
	
	display ""
	
	return steps
}

let cycles = 0 -> 3 map find_period

display rep cycles

let result = lcm cycles

display ""

put result
