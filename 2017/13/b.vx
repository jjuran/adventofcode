#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let modular = import "modular"

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = eval "[" str((load input_path).lines() map { v "," }) "]"

let max_range = Iter.max (data map .value)

var T = 0
var M = 1  # supermodulus

def get_missing (n, set)
{
	var x = "x" * n
	
	for e in set do
	{
		x[ e ] = '\0'
	}
	
	return (begin x).find('x').past.length
}

for range in 2 .. max_range do
{
	let prime = range - 1
	
	let set = data ver { v.value == range }
	
	if set.length == prime - 1 then
	{
		let wrap = set map { v.key mod (prime * 2) / 2 }
		
		let m  = prime
		let dt = get_missing (prime, wrap)
		
		let delay = -(T + dt) mod m
		
		T += modular.alignment (delay, m, M)
		M *= m
	}
}

def caught (delay)
{
	for kv in data do
	{
		let (depth, range) = *kv
		
		let modulus = (range - 1) * 2
		
		if (delay + depth) % modulus == 0 then
		{
			return true
		}
	}
	
	return false
}

var delay = T * 2

while caught delay do
{
	delay += M
}

put delay
