#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const digraph = import "digraph"

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

def parse (line)
{
	var p = (begin line).find ' '
	
	const src = int p.past
	
	p += " <-> "
	
	const dst = [eval p.rest]
	
	return src => dst
}

const data = (load input_path).lines() map parse

var graph = int^data

var n_groups = 0

while graph do
{
	++n_groups
	
	const first = graph.keys[ 0 ]
	
	const closure = digraph.closure( graph, first )
	
	for c in closure do
	{
		graph[ c ] = []
	}
	
	graph = int^(graph ver .value)
}

print n_groups
