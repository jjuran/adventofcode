#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const data = "1113122113"

var memo = str^[]

def split
{
	const s (str) = _
	
	var result = []
	
	var p = begin s
	
	var q = p
	
	while '2' in p.rest do
	{
		while *p++ != '2' do {}
		
		const rest = p.rest
		
		if substr( rest, 0, 2 ) == "31"
		or substr( rest, 0, 3 ) in ["131", "132"]
		or substr( rest, 0, 4 ) == "1113" then
		{
			result <-- p - q
			
			q = p
		}
	}
	
	result <-- q.rest
	
	return result
}

def look-and-say
{
	const s (str) = _
	
	if s in memo then
	{
		return *memo[ s ]
	}
	
	var result = ""
	
	var p = begin s
	
	while p do
	{
		const q = p
		const c = *p++
		
		while p and *p == c do
		{
			++p
		}
		
		result = str( result, (p - q).length, c )
	}
	
	return *(memo[ s ] = split result)
}

var x = [data]
var n

for i in 1 .. 50 do
{
	x = x map look-and-say
	
	n = (str x).length
	
	display (i ": " n)
}

display ""

print n
