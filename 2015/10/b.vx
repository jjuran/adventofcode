#!/usr/bin/env vx

const data = "1113122113"

var memo = str^[]

def split
{
	const s (str) = _
	
	var result = []
	
	var p = begin s
	
	var q = p
	
	while p = p.find '2' do
	{
		++p
		
		const rest = p.rest
		
		if substr( rest, 0, 2 ) == "31" then
		{
			result <-- p - q
			
			q = p
		}
	}
	
	result <-- q.rest
	
	return result
}

def look-and-say
{
	const s (str) = _
	
	if s in memo then
	{
		return *memo[ s ]
	}
	
	var result = ""
	
	var p = begin s
	
	while p do
	{
		const q = p
		const c = *p++
		
		p += c*
		
		result .= (p - q).length c ""
	}
	
	return *(memo[ s ] = split result)
}

var x = [data => 1]

for i in 1 .. 50 do
{
	var freq = str^[]
	
	for element in x do
	{
		let results = look-and-say element.key
		
		for r in [results] do
		{
			if not r in freq then
			{
				freq[ r ] = 0
			}
			
			freq[ r ] += element.value
		}
	}
	
	x <- freq
}

let n = x map { v.key.length * v.value } per Math.sum

print n
