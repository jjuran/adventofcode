#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

var signals = str^[]
var circuit = str^[]

for instruction in data do
{
	let words = [instruction / ' ']
	
	var n = words.length
	
	let wire = words[ --n ]
	
	let input = words[ 0 -> --n ]
	
	circuit[ wire ] = input
}

def NOT (x) { u16.decode ~(u16.encode x) }

def AND (a, b)
{
	return u16.decode (u16.encode a & u16.encode b)
}

def OR (a, b)
{
	return u16.decode (u16.encode a | u16.encode b)
}

def LSHIFT (signal, n)
{
	return u16 (signal * 2^n)
}

def RSHIFT (signal, n)
{
	return half^n signal
}

let binary-ops = str^
[
	AND: AND,
	OR: OR,
	LSHIFT: LSHIFT,
	RSHIFT: RSHIFT,
]

def chase (trace)
{
	try
	{
		return int trace
	}
	catch {}
	
	let wire = trace
	
	if wire in signals then
	{
		return signals[ wire ]
	}
	
	let input = circuit[ wire ]
	
	let n = input.length
	
	if n == 1 then
	{
		let signal = chase input[ 0 ]
		
		return signals[ wire ] = signal
	}
	
	if n == 2 then
	{
		let signal = NOT chase input[ 1 ]
		
		return signals[ wire ] = signal
	}
	
	let operator = input[ 1 ]
	
	let a = chase input[ 0 ]
	let b = chase input[ 2 ]
	
	let signal = binary-ops[ operator ] (a, b)
	
	return signals[ wire ] = signal
}

let signal = chase "a"

signals = str^[ b: signal ]

put chase "a"
