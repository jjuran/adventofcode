#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let N = data.length

var RF = [0, 0]  # register file
var IP = 0       # instruction pointer

def TPL (reg)
{
	RF[ reg ] *= 3
}

def HLF (reg)
{
	RF[ reg ] = RF[ reg ] div 2
}

def INC (reg)
{
	++RF[ reg ]
}

def JMP (offset)
{
	IP += offset - 1
}

def JIE (reg, offset)
{
	if RF[ reg ] % 2 == 0 then
	{
		IP += offset - 1
	}
}

def JIO (reg, offset)
{
	if RF[ reg ] == 1 then
	{
		IP += offset - 1
	}
}

let digit = '0' .. '9'

let num = ['+', digit+ => int] | (['-', digit+] => int)
let reg = 'a' .. 'b' => {u8 *_ - u8 'a'}

let hlf = ["hlf ", reg]            => HLF
let tpl = ["tpl ", reg]            => TPL
let inc = ["inc ", reg]            => INC
let jmp = ["jmp ",            num] => JMP
let jie = ["jie ", reg, ", ", num] => JIE
let jio = ["jio ", reg, ", ", num] => JIO

let illegal = ~byteclass()* => { throw "Illegal instruction: " _ }

let grammar = hlf | tpl | inc | jmp | jie | jio | illegal

var A = 0
var B = 0

while IP < N do
{
	var p = begin data[ IP++ ]
	
	p += grammar
}

put RF[ 1 ]  # print the value in register `b`
