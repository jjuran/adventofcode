#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let T = str => int

var stats = str^[]

var distance = str^[]
var points   = str^[]

var max-distance = 0

for row in data do
{
	let words = [row[ 0 -> row.length - 1 ] / ' ']
	
	let name    = words[ 0 ]
	let speed   = int words[ 3 ]
	let stamina = int words[ 6 ]
	let rest    = int words[ 13 ]
	
	stats[ name ] = record( v: speed, d: stamina, r: rest )
	
	distance[ name ] = 0
	points[ name ]   = 0
}

for t in 0 -> 2503 do
{
	for x in stats do
	{
		let rec = x.value
		
		let period = rec.d + rec.r
		
		if t mod period < rec.d then
		{
			max-distance = Math.max( max-distance, (distance[ x.key ] += rec.v) )
		}
	}
	
	for y in stats do
	{
		if distance[ y.key ] == max-distance then
		{
			++points[ y.key ]
		}
	}
}

let most = points map .value per Math.max

put most 
