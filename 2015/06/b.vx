#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let grid-row = record( size: 1000, encode: ..., decode: ... )

var light-grid = vector[ grid-row ] 1000

def point (x_comma_y)
{
	return [x_comma_y / ','] map int
}

def get-coords (xy_through_xy)
{
	let parts = [xy_through_xy / ' '][[0, 2]]
	
	return parts map point
}

def turn-on (row, x0, x1)
{
	#assert not '\xff' in row[ x0 -> x1 ]
	
	let affected = row[ x0 -> x1 ] map {byte (u8 _ + 1)}
	
	return row[ 0 -> x0 ] (packed affected) row[ x1 -> 1000 ]
}

def turn-off (row, x0, x1)
{
	let affected = row[ x0 -> x1 ] map {_ and byte (u8 _ - 1)}
	
	return row[ 0 -> x0 ] (packed affected) row[ x1 -> 1000 ]
}

def toggle (row, x0, x1)
{
	#assert not '\xff' in row[ x0 -> x1 ]
	#assert not '\xfe' in row[ x0 -> x1 ]
	
	let affected = row[ x0 -> x1 ] map {byte (u8 _ + 2)}
	
	return row[ 0 -> x0 ] (packed affected) row[ x1 -> 1000 ]
}

let n  = data.length
var ii = 0

for step in data do
{
	let action = ???
	let target = ???
	
	let c = step[ 6 ]
	
	if c == 'f' then
	{
		action = turn-off
		target = substr( step, 9 )
	}
	else if c == 'n' then
	{
		action = turn-on
		target = substr( step, 8 )
	}
	else
	{
		action = toggle
		target = substr( step, 7 )
	}
	
	let coords = get-coords target
	
	let (pt0, pt1) = *coords
	
	let (x0, y0) = *pt0
	let (x1, y1) = *pt1
	
	display (++ii "/" n)
	
	for i in y0 .. y1 do
	{
		light-grid[ i ] = action( light-grid[ i ], x0, x1 + 1 )
	}
}

display ""

var total = 0

for i in 0 -> 1000 do
{
	let row = light-grid[ i ]
	
	for j in row do
	{
		total += u8 j
	}
}

put total
