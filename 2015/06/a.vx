#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vsl" }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let grid-row = record( size: 1000, encode: ..., decode: ... )

var light-grid = vector[ grid-row ] 1000

def point (x_comma_y)
{
	return [x_comma_y / ','] map int
}

def get-coords (xy_through_xy)
{
	let parts = [xy_through_xy / ' '][[0, 2]]
	
	return parts map point
}

def make-mask (x0, x1)
{
	return (x"00" * x0) (x"FF" * (x1 - x0)) (x"00" * (1000 - x1))
}

def turn-on (a, b)
{
	return a | b
}

def turn-off (a, b)
{
	return a & ~b
}

def toggle (a, b)
{
	return a ~ b
}

let n = data.length
var ii = 0

for step in data do
{
	let action = ???
	let target = ???
	
	let c = step[ 6 ]
	
	if c == 'f' then
	{
		action = turn-off
		target = substr( step, 9 )
	}
	else if c == 'n' then
	{
		action = turn-on
		target = substr( step, 8 )
	}
	else
	{
		action = toggle
		target = substr( step, 7 )
	}
	
	let coords = get-coords target
	
	let (pt0, pt1) = *coords
	
	let (x0, y0) = *pt0
	let (x1, y1) = *pt1
	
	let mask = make-mask( x0, x1 + 1 )
	
	display (++ii "/" n)
	
	for i in y0 .. y1 do
	{
		light-grid[ i ] = action( light-grid[ i ], mask )
	}
}

display ""

var total = 0

for i in 0 -> 1000 do
{
	total += Iter.popcount light-grid[ i ]
}

put total
