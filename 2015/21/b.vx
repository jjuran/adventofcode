#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let digit = '0' .. '9'
let upper = 'A' .. 'Z'
let lower = 'a' .. 'z'
let alpha = upper | lower
let alnum = alpha | digit
let title = (alpha | ' ')+ => str
let value = digit+ => int
let field = [title, ": ", value] => { head _ => tail _ }
let sheet = [field, "\n"]+ => { str^[ _ ] }

def parse_stats (text)
{
	var p = begin text
	
	return p += sheet
}

let boss_stats_table = parse_stats load input_path

let boss_stats = boss_stats_table[[ "Hit Points", "Damage", "Armor" ]]

def stat (s)
{
	var p = begin s
	
	p += ' '*
	
	return p += digit+ => int
}

def subparse_aisle (headings, listings: ...)
{
	var column_names   = []
	var column_offsets = []
	
	var start = begin headings
	
	var p = start
	p = p.find ':'
	
	let section = p - start
	
	p += ':'
	
	while p += ' '+ do
	{
		column_offsets <-- p.past.length
		column_names   <-- (p += alpha+ => str)
	}
	
	let n_columns = column_names.length
	
	let offsets = column_offsets
	
	let items = [listings] map
	{
		let listing = v
		
		let name = listing[ 0 -> offsets[ 0 ] ]
		
		let stats = 0 -> n_columns map { stat substr (listing, offsets[ v ]) }
		
		name => stats,
	}
	
	return section => items
}

let heads =  [upper, (alnum | ' ' | ':')+, '\n'] => str
let items = ([upper, (alnum | ' ' | '+')+, '\n'] => str)+
let aisle = ['\n', heads, items] => subparse_aisle
let store = aisle+

def parse_store (text)
{
	var p = begin text
	
	return p += store
}

let (weapons, armors, rings) = *([parse_store DATA] map .value)

let Player = 0

def battle (stats)
{
	var whose_turn = Player
	
	def damage (attacker, defender)
	{
		let damage = stats[ attacker ][ 1 ]
		let armor  = stats[ defender ][ 2 ]
		
		return Math.max (damage - armor, 1)
	}
	
	def turn ()
	{
		let attacker = whose_turn
		let defender = 1 - whose_turn
		
		let damaged = damage (attacker, defender)
		
		let hp = stats[ defender ][ 0 ] -= damaged
		
		if hp <= 0 then
		{
			return attacker
		}
		
		whose_turn = defender
		
		return -1
	}
	
	while (var winner = turn()) < 0 do {}
	
	return winner
}

def boss_battle (damage, armor)
{
	let player_stats = [100, damage, armor]
	
	return battle [ player_stats, boss_stats ]
}

let n_weapons = 5
let n_armors  = 6  # because it's optional
let n_rings   = 7  # ditto

let max_configs = n_weapons * n_armors * n_rings^2

var losses = []

for config in 0 -> max_configs do
{
	var x = config
	
	let R1 = x mod n_rings - 1;  x = x div n_rings
	let R2 = x mod n_rings - 1;  x = x div n_rings
	
	if R2 >= 0 and R2 >= R1 then
	{
		continue
	}
	
	let Ar = x mod n_armors - 1;  x = x div n_armors
	let We = x
	
	let weapon = weapons[ We ]
	let armor  = if Ar >= 0 then {armors[ Ar ]}
	let ring1  = if R1 >= 0 then {rings[ R1 ]}
	let ring2  = if R2 >= 0 then {rings[ R2 ]}
	
	let outfit = [ weapon, armor, ring1, ring2 ]
	
	let stats = 0 -> 3 map { let i = v; outfit map { v.value[ i ] } per Math.sum }
	
	let winner = boss_battle (*stats[ 1 .. 2 ])
	
	if winner != Player then
	{
		losses <-- stats[ 0 ]
	}
}

put Iter.max losses

end.

Weapons:    Cost  Damage  Armor
Dagger        8     4       0
Shortsword   10     5       0
Warhammer    25     6       0
Longsword    40     7       0
Greataxe     74     8       0

Armor:      Cost  Damage  Armor
Leather      13     0       1
Chainmail    31     0       2
Splintmail   53     0       3
Bandedmail   75     0       4
Platemail   102     0       5

Rings:      Cost  Damage  Armor
Damage +1    25     1       0
Damage +2    50     2       0
Damage +3   100     3       0
Defense +1   20     0       1
Defense +2   40     0       2
Defense +3   80     0       3
