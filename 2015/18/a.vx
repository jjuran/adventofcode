#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = load input_path

const width = ((begin data).find '\n').past.length
const stride = width + 1

const blank_line = (" " * width) "\n"

var grid (str) = "\r" blank_line data blank_line

const neighbor_offsets = -1 .. 1 map { v - stride, v, v + stride } ver ...

def count_neighbors (index)
{
	return neighbor_offsets pop {grid[ index + v ] == '#'}
}

def step
{
	var new_grid (str) = grid
	
	for i in 0 -> width do
	{
		for j in 0 -> width do
		{
			const index (int) = 1 + j + (1 + i) * stride
			
			const n (int) = count_neighbors index
			
			if n != 2 then
			{
				new_grid[ index ] = ".#"[+(n == 3)]
			}
		}
	}
	
	grid = take new_grid
}

for i in 0 -> 100 do
{
	display i "/100"
	
	step()
}

const count = grid pop {v == '#'}

display ""

print count
