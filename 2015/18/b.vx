#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vsl" }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let width = ((begin data).find '\n').past.length
let stride = width + 1

let blank_line = (" " * width) "\n"

var grid = "\r" blank_line data blank_line

let neighbor_offsets = -1 .. 1 map { v - stride, v, v + stride } ver ...

def count_neighbors (index)
{
	return neighbor_offsets pop {grid[ index + v ] == '#'}
}

let corner_indices = [1, width] map {v + stride, v + stride * width}

for corner in corner_indices do
{
	grid[ corner ] = '#'
}

def step
{
	var new_grid = grid
	
	for j in 0 -> width do
	{
		for i in 0 -> width do
		{
			let index = 1 + i + (1 + j) * stride
			
			let n = count_neighbors index
			
			if n != 2 then
			{
				new_grid[ index ] = ".#"[+(n == 3)]
			}
		}
	}
	
	grid = take new_grid
	
	for i in corner_indices do
	{
		grid[ i ] = '#'
	}
}

for i in 0 -> 100 do
{
	display i "/100"
	
	step()
}

let count = grid pop {v == '#'}

display ""

put count
