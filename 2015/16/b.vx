#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let scan = str^(DATA.lines() map eval)

def parse (s)
{
	var p = begin s
	
	p += "Sue ".length
	
	var q = p.find ':'
	
	let i = int (q - p)
	
	q += ": ".length
	
	return i => str^[eval q.rest]
}

def matching (sue)
{
	let details = sue.value
	
	for compound in scan do
	{
		let key = compound.key
		
		if not key in details then
		{
			continue
		}
		
		var cmp = compound.value <=> details[ key ]
		
		if key in ["cats", "trees"] then
		{
			++cmp
		}
		else if key in ["pomeranians", "goldfish"] then
		{
			--cmp
		}
		
		if cmp != 0 then
		{
			return false
		}
	}
	
	return true
}

let matches = data map parse ver matching

put matches[ 0 ].key

end.
children: 3
cats: 7
samoyeds: 2
pomeranians: 3
akitas: 0
vizslas: 0
goldfish: 5
trees: 3
cars: 2
perfumes: 1
