#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const modular = import "modular"

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const digit = '0' .. '9'
const number = digit+ => int

var p = begin load input_path

p = p.find "row"

const (row, col) = p += ["row ", number, ", column ", number]

def summation (max)
{
	let mid = max div 2
	let odd = max mod 2
	
	return (1 + odd + max) * mid + odd
}

def diagonalize (row, col)
{
	--row
	--col
	
	return col + summation (row + col)
}

const seed    = 20151125
const factor  = 252533
const modulus = 33554393

def generate_nth_code (index)
{
	return seed * modular.power( factor, index, modulus ) % modulus
}

print generate_nth_code diagonalize (row, col)
