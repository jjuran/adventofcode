#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const data = "hxbxwxba"

var report = null

const throttle = thread
{
	while true do
	{
		report = display
		
		sleep 0.05  # 20fps
	}
}

def two-pair (word)
{
	var first-pair = '\0'
	var last-char  = '\0'
	
	for c in word do
	{
		const same = c == last-char
		
		last-char = c
		
		if same then
		{
			if first-pair and first-pair != c then
			{
				return true
			}
			
			first-pair = c
		}
	}
	
	return false
}

def straight (word)
{
	var ups = 0
	
	var last-char = '\0'
	
	for c in word do
	{
		if u8 c - 1 == u8 last-char then
		{
			if ++ups == 2 then
			{
				return true
			}
		}
		else
		{
			ups = 0
		}
		
		last-char = c
	}
	
	return false
}

def secure (password)
{
	return straight password and two-pair password
}

def increment (word)
{
	var i = word.length
	
	while word[ --i ] == 'z' do
	{
		word[ i ] = 'a'
	}
	
	var c = word[ i ]
	
	do
	{
		c = byte (u8 c + 1)
	}
	while c in "iol"
	
	word[ i ] = c
	
	return word
}

var password = data

while not secure password do
{
	report password
	report = null
	
	password = increment password
}

display ""

print password

exit 0
