#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const data = "hxbxwxba"

def two-pair
{
	const word = _
	
	var first-pair = '\0'
	var last-char  = '\0'
	
	for c in word do
	{
		const same = c == last-char
		
		last-char = c
		
		if same then
		{
			if first-pair and first-pair != c then
			{
				return true
			}
			
			first-pair = c
		}
	}
	
	return false
}

def straight
{
	const word = _
	
	var ups = 0
	
	var last-char = '\0'
	
	for c in word do
	{
		if u8 c - 1 == u8 last-char then
		{
			if ++ups == 2 then
			{
				return true
			}
		}
		else
		{
			ups = 0
		}
		
		last-char = c
	}
	
	return false
}

def secure
{
	const password = _
	
	for c in "iol" do
	{
		if c in password then
		{
			return false
		}
	}
	
	return straight password and two-pair password
}

def increment (word)
{
	var i = word.length
	
	while word[ --i ] == 'z' do
	{
		word[ i ] = 'a'
	}
	
	word[ i ] = byte (u8 word[ i ] + 1)
	
	return word
}

var password = data

def next (password)
{
	do
	{
		display password
		
		password = increment password
	}
	while not secure password
	
	return password
}

password = next^2 password

display ""

print password
