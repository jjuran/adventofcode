#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let digit = '0' .. '9'
let card = digit+ => int
let deck = [card, '\n']+ => {[_]}
let hand = ["Player ", digit, ":\n", deck, '\n'?]
let grammar = [hand (*) 2] => {[_]}

def parse
{
	var p = begin data
	
	return p += grammar
}

var decks = parse data

var (one, two) = *decks

def play_game (one, two)
{
	def play_round
	{
		let a = one[ 0 ]
		let b = two[ 0 ]
		
		one = [tail *one]
		two = [tail *two]
		
		if a > b then
		{
			one <-- a, b
		}
		else
		{
			two <-- b, a
		}
	}
	
	while one and two do
	{
		play_round()
	}
	
	return one, two
}

one, two = play_game (one, two)

def score (deck)
{
	let n = deck.length
	
	deck <-- 0
	deck = -deck
	
	return 0 .. n map { deck[ v ] * v } per Math.sum
}

let winning_score = score one + score two
put winning_score
