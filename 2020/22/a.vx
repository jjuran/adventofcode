#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = load input_path

const digit = '0' .. '9'
const card = digit+ => int
const deck = [card, '\n']+ => {[_]}
const hand = ["Player ", digit, ":\n", deck, '\n'?]
const grammar = [hand (*) 2] => {[_]}

def parse
{
	var p = begin data
	
	return p += grammar
}

var decks = parse data

var (one, two) = *decks

def play_game (one, two)
{
	def play_round
	{
		const a = one[ 0 ]
		const b = two[ 0 ]
		
		one = [tail *one]
		two = [tail *two]
		
		if a > b then
		{
			one <-- a, b
		}
		else
		{
			two <-- b, a
		}
	}
	
	while one and two do
	{
		play_round()
	}
	
	return one, two
}

one, two = play_game (one, two)

def score (deck)
{
	const n = deck.length
	
	deck <-- 0
	deck = -deck
	
	return 0 .. n map { deck[ v ] * v } per Math.sum
}

const winning_score = score one + score two
print winning_score
