#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let display = (import "status") { OUT <== _ }

let data = if argc > 1 then {argv[ 1 ]} else {"10,16,6,0,1,17"}

let starting = [data / ','] map int

let N = 30 * 10^6

var seen = vector[u32] N

let n = starting.length

for T0 in 1 -> n do
{
	seen[ starting[ T0 - 1 ] ] = T0
}

var next = starting[ n - 1 ]

for T in n -> N do
{
	if T mod 2^13 == 0 then
	{
		display (T div 2^13) "/" (N div 2^13)
	}
	
	let t = seen[ next ] or T
	
	seen[ next ] = T
	
	next = T - t
}

display ""

put next
