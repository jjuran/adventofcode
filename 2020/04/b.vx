#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let digit = '0' .. '9'
let lower = 'a' .. 'z'
let alnum = digit | lower
let vchar = alnum | '#'
let space = ' ' | '\n'
let xdigit = digit | 'a' .. 'f'

let field = [lower (*) 3] => str
let value = vchar+ => str

let pair = [field, ":", value] => {head _ => tail _}
let row = [pair, [space, pair]*] => {[_]}
let set = [row,  ["\n\n", row]*] => {[_]}

let fields = DATA.lines()

def year_within (range)
{
	return lambda (v)
	{
		return v.length == 4 and int v in range
	}
}

def matches (pattern)
{
	return lambda (v)
	{
		var p = begin v
		
		return (+) (p += pattern) and not p
	}
}

def height (v)
{
	var p = begin v
	
	if let num = p += digit+ => int then
	{
		let unit = p.rest
		
		if unit == "cm" then
		{
			return num in 150 .. 193
		}
		
		if unit == "in" then
		{
			return num in 59 .. 76
		}
	}
	
	return false
}

let validation = str^
[
	byr: year_within (1920 .. 2002),
	iyr: year_within (2010 .. 2020),
	eyr: year_within (2020 .. 2030),
	hgt: height,
	hcl: matches ['#', xdigit (*) 6],
	ecl: { _ in ["amb blu brn gry grn hzl oth" / ' '] },
	pid: matches [digit (*) 9],
	cid: {true},
]

def is_valid (passport)
{
	let keys = passport map .key
	
	let missing = fields ver { not v in keys }
	
	if not missing then
	{
		let malformed = passport ver { not validation[ v.key ] v.value }
		
		return not malformed
	}
	
	return false
}

var p = begin data

let passports = p += set

let valid_passports = passports ver is_valid

put valid_passports.length

end.
byr
iyr
eyr
hgt
hcl
ecl
pid
