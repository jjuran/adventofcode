#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path).lines()

const digit = ('0' .. '9') => int

const ops = byte^['*': Math.product, '+': Math.sum]

var p;

def calc
{
	def term
	{
		const x
		
		if *p == '(' then
		{
			++p
			x = calc()
			p += ')'
		}
		else
		{
			x = p += digit
		}
		
		return x
	}
	
	var x = term()
	
	var last_op = '~'
	
	while p and *p != ')' do
	{
		p += ' '
		
		const this_op = *p++
		
		p += ' '
		
		var q = p
		
		var y = term()
		
		if not p or *p == ')' then
		{
			x = ops[ this_op ](x, y)
			break
		}
		
		p += ' '
		
		const next_op = *p
		
		if u8 next_op > u8 this_op then
		{
			p = q
			
			y = calc()
		}
		
		x = ops[ this_op ](x, y)
	}
	
	return x
}

def calculate (expr)
{
	p = begin expr
	
	return calc p
}

const sum = data map calculate per Math.sum
print sum
