#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let n = data.length

let lines = data.lines()

let width = lines[ 0 ].length
let stride = width + 1
let height = lines.length

let seats = 0 -> n ver { data[ v ] == 'L' }

def find_neighbors (i)
{
	display "" i
	
	let x = i mod stride
	let y = i div stride
	
	let xs = [x - 1, x, x + 1] ver {v in 0 -> width}
	let ys = [y - 1, y, y + 1] ver {v in 0 -> height}
	
	let nearby = ys map {v * stride} map { let y = v; *(xs map {v + y}) }
	
	return nearby ver {v != i and data[ v ] == 'L'}
}

let neighbor_map = seats map { v => find_neighbors v }

var grid = data

def step
{
	var work = grid
	
	for seat in neighbor_map do
	{
		let i = seat.key
		
		let popcount = seat.value pop {grid[ v ] == '#'}
		
		work[ i ] = ("#" grid[ i ] "LL")[ (popcount + 2) div 3 ]
	}
	
	grid <-> work
	
	return grid != work
}

var steps = 0

display "0"

while step() do
{
	display "" (grid pop {v == '#'})
}

put ""
