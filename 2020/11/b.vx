#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = load input_path

const n = data.length

const lines = data.lines()

const width = lines[ 0 ].length
const stride = width + 1
const height = lines.length

const seats = 0 -> n ver { data[ v ] == 'L' }

const moves =
[
	[-1, -1], [0, -1], [1, -1],
	[-1,  0],          [1,  0],
	[-1,  1], [0,  1], [1,  1],
]

def find_seat (x, y, dx, dy)
{
	x += dx
	y += dy
	
	while x in 0 -> width and y in 0 -> height do
	{
		const i = y * stride + x
		
		if data[ i ] == 'L' then
		{
			return i
		}
		
		x += dx
		y += dy
	}
	
	return ()
}

def find_neighbors (i)
{
	display "" i
	
	const x = i mod stride
	const y = i div stride
	
	return moves map { find_seat (x, y, *v) }
}

const neighbor_map = seats map { v => find_neighbors v }

var grid = data

def step
{
	var work = grid
	
	for seat in neighbor_map do
	{
		const i = seat.key
		
		const popcount = seat.value pop {grid[ v ] == '#'}
		
		work[ i ] = ("#" grid[ i ] "L")[ (popcount + 3) div 4 ]
	}
	
	grid <-> work
	
	return grid != work
}

display "0"

while step() do
{
	display "" (grid pop {v == '#'})
}

print ""
