#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

var maskdata
var metamask

var memory = int^[]

def set_mask (mask)
{
	maskdata =   unbin translated (mask, "X",  "0")
	metamask = ~(unbin translated (mask, "X0", "01"))
}

def store (addr, data)
{
	let bytes = (x"00" * (5 - data.size)) data  # left-pad with NUL bytes
	
	data = unhex "0x" hex (bytes & metamask | maskdata)
	
	memory[ addr ] = data
}

for line in data do
{
	let (dst, eq, src) = line / ' '
	
	if dst == "mask" then
	{
		set_mask src
	}
	else
	{
		let p = (begin dst + 4)
		
		let q = p.find ']'
		
		let addr = int (q - p)
		let data = unhex hex int src
		
		store (addr, data)
	}
}

let sum = memory.values per Math.sum
put sum
