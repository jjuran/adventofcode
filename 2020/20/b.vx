#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

let import = { let path = libdir _ ".vsl"; eval( load path, path ) }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = load input_path

let edge = 10

let digit = '0' .. '9'
let number = digit+ => int
let heading = ["Tile ", number, ":\n"]
let cell = '.' | '#'
let row = [cell+, '\n']
let grid = [row+ => str]
let tile = [heading, grid, '\n'] => {head _ => tail _}
let grammar = tile+ => {[_]}

def parse
{
	var p = begin data
	
	return p += grammar
}

var tileset = int^parse()

def canon (edge)
{
	return Math.min (edge, -edge)
}

def top_edge (grid)
{
	return grid[ 0 -> edge ]
}

def bottom_edge (grid)
{
	return (-grid)[ 1 .. edge ]
}

def left_edge (grid)
{
	return -mince( grid, edge + 1 )
}

def right_edge (grid)
{
	return -mince( substr (-grid, 1), edge + 1 )
}

let each_edge = top_edge, left_edge, bottom_edge, right_edge

def map_edges
{
	var edges = []
	
	for t in tileset do
	{
		edges <-- *([each_edge t.value] map { canon v => t.key })
	}
	
	var edge_map = str^[]
	
	for e1 in edges do
	{
		edge_map[ e1.key ] = []
	}
	
	for e2 in edges do
	{
		edge_map[ e2.key ] <-- e2.value
	}
	
	return edge_map
}

let adjacency = map_edges()

let shared_adjacency = adjacency ver { v.value.length > 1 }  # not map edges

def score_tiles_by_unique_edges
{
	var scores = int^[]
	
	for adj in adjacency do
	{
		if adj.value.length == 1 then
		{
			let tile_id = *adj.value
			
			if tile_id in scores then
			{
				++scores[ tile_id ]
			}
			else
			{
				scores[ tile_id ] = 1
			}
		}
	}
	
	return [*scores]
}

let scores = score_tiles_by_unique_edges()

let surveys = int^(tileset map { v.key => [each_edge v.value] })

def find_edge_in_edges (edge, edges)
{
	let edge_indices = 0 .. 3 map { let e = edges[ v ]; e => v, -e => v }
	
	return (str^edge_indices)[ edge ]
}

let tiles_per_edge = (scores pop { v.value == 1 }) / 4 + 2
let tiles_per_grid = tiles_per_edge^2

var tile_grid = [null (*) tiles_per_grid]

def vflip (grid)
{
	return "\n".join (*-grid.lines(), "")
}

def hflip (grid)
{
	return "\n".join (*(grid.lines() map {-v}), "")
}

def flip2 (grid)
{
	return "\n".join (*(-grid.lines() map {-v}), "")
}

def transpose (grid)
{
	let lines = grid.lines()
	
	let n = lines[ 0 ].length + 1
	
	return 0 -> n map { mince( substr( grid, v ), n ) "\n" } per str
}

let grid_transforms =
[
	...,    # no-op
	vflip,  # vflip only
	hflip,  # hflip only
	flip2,  # half-circle rotation
	transpose,          # transposed
	transpose * hflip,  # transposed vflip
	transpose * vflip,  # transposed hflip
	transpose * flip2,  # transposed half-circle rotation
]

let index_transforms =
[
	[0, 1, 2, 3],  # no-op
	[2, 1, 0, 3],  # vflip only
	[0, 3, 2, 1],  # hflip only
	[2, 3, 0, 1],  # half-circle rotation
	[1, 0, 3, 2],  # transposed
	[3, 0, 1, 2],  # transposed vflip
	[1, 2, 3, 0],  # transposed hflip
	[3, 2, 1, 0],  # transposed half-circle rotation
]

def transformed_edge_index (index, transform)
{
	return index_transforms[ transform ][ index ]
}

def find_other_tile_with_shared_edge (edge)
{
	let previous = tile_grid ver ... map .key
	let adjacent = adjacency[ canon edge ] ver { not v in previous }
	
	return *adjacent
}

var n_tiles_solved = 1

def solve_first_corner
{
	let Top    = 0
	let Left   = 1
	let Bottom = 2
	let Right  = 3
	
	let corner_tile_id = (scores ver { v.value == 2 })[ 0 ].key
	
	let unique_edges = adjacency ver { v.value == [corner_tile_id] }
	                             map .key
	
	let edges = surveys[ corner_tile_id ]
	let found = unique_edges map { find_edge_in_edges (v, edges) }
	
	let vflipped = Bottom in found
	let hflipped = Right  in found
	
	let transform = +vflipped + 2 * +hflipped
	
	tile_grid[ 0 ] = corner_tile_id => transform
}

def solve_edge (start, inc)
{
	let Top    = 0
	let Left   = 1
	let Bottom = 2
	let Right  = 3
	
	let Near
	let Far
	let Adjacent
	
	if inc == 1 then
	{
		Near = Left
		Far = Right
		Adjacent = [Top, Bottom]
	}
	else
	{
		Near = Top
		Far = Bottom
		Adjacent = [Left, Right]
	}
	
	var x = start
	
	let src = tile_grid[ x ]
	
	var next_tile = src.key
	var transform = src.value
	
	var previous_edges = surveys[ next_tile ]
	
	for i in 1 -> tiles_per_edge - 1 do
	{
		let far_edge = previous_edges[ transformed_edge_index (Far, transform) ]
		
		next_tile = find_other_tile_with_shared_edge far_edge
		
		var next_edges = surveys[ next_tile ]
		
		let unique_edge = *(adjacency ver { v.value == [next_tile] } map .key)
		
		assert (+) unique_edge == 1
		
		var shared_edge_index = find_edge_in_edges (far_edge,    next_edges)
		var unique_edge_index = find_edge_in_edges (unique_edge, next_edges)
		
		var vflipped = (unique_edge_index > 1) == (start == 0)
		var hflipped = shared_edge_index > 1
		
		if inc != 1 then
		{
			vflipped <-> hflipped
		}
		
		let transposed = shared_edge_index in Adjacent
		
		transform = +vflipped + 2 * +hflipped + 4 * +transposed
		
		tile_grid[ x += inc ] = next_tile => transform
		
		previous_edges <- next_edges
		
		display "" ++n_tiles_solved
	}
}

def solve_corner (end, inc, last=false)
{
	let Top    = 0
	let Left   = 1
	let Bottom = 2
	let Right  = 3
	
	let Near
	let Far
	let Adjacent
	
	if inc == 1 then
	{
		Near = Left
		Far = Right
		Adjacent = [Top, Bottom]
	}
	else
	{
		Near = Top
		Far = Bottom
		Adjacent = [Left, Right]
	}
	
	let dst = end - inc
	let src = dst - inc
	
	let placing = tile_grid[ src ]
	
	var tile_id   = placing.key
	var transform = placing.value
	
	let previous_edges = surveys[ tile_id ]
	
	let far_edge = previous_edges[ transformed_edge_index (Far, transform) ]
	
	let next = find_other_tile_with_shared_edge far_edge
	
	let next_edges = surveys[ next ]
	
	let nearby_edges = shared_adjacency ver { next in v.value } map .key
	
	let nearby_edge = *(nearby_edges ver { v != canon far_edge })
	
	assert (+) nearby_edge == 1
	
	var shared_edge_index = find_edge_in_edges (far_edge,    next_edges)
	var nearby_edge_index = find_edge_in_edges (nearby_edge, next_edges)
	
	var vflipped = (nearby_edge_index < 2) != last
	var hflipped =  shared_edge_index > 1
	
	if inc != 1 then
	{
		vflipped <-> hflipped
	}
	
	let transposed = shared_edge_index in Adjacent
	
	transform = +vflipped + 2 * +hflipped + 4 * +transposed
	
	tile_grid[ dst ] = next => transform
	
	display "" ++n_tiles_solved
}

def solve_center (index)
{
	let Top    = 0
	let Left   = 1
	let Bottom = 2
	let Right  = 3
	
	let above = tile_grid[ index - tiles_per_edge ]
	let aside = tile_grid[ index - 1 ]
	
	let above_edges = surveys[ above.key ]
	let aside_edges = surveys[ aside.key ]
	
	let above_edge = above_edges[ transformed_edge_index (Bottom, above.value) ]
	let aside_edge = aside_edges[ transformed_edge_index (Right,  aside.value) ]
	
	let next = find_other_tile_with_shared_edge above_edge
	
	let next_edges = surveys[ next ]
	
	let nearby_edges = adjacency ver { next in v.value } map .key
	
	let top_edge  = *(nearby_edges ver { v == canon above_edge })
	let left_edge = *(nearby_edges ver { v == canon aside_edge })
	
	assert (+) top_edge == 1
	assert (+) left_edge == 1
	
	var top_edge_index  = find_edge_in_edges (top_edge,  next_edges)
	var left_edge_index = find_edge_in_edges (left_edge, next_edges)
	
	var vflipped = top_edge_index  > 1
	var hflipped = left_edge_index > 1
	
	let transposed = top_edge_index in [Left, Right]
	
	let transform = +vflipped + 2 * +hflipped + 4 * +transposed
	
	tile_grid[ index ] = next => transform
	
	display "" ++n_tiles_solved
}

solve_first_corner()

solve_edge (0, 1)  # solve top edge

solve_edge (0, tiles_per_edge)  # solve left edge

solve_corner (tiles_per_edge, 1)  # solve top right corner
solve_corner (tiles_per_edge^2, tiles_per_edge)  # bottom left corner

solve_edge (tiles_per_edge - 1, tiles_per_edge)  # solve right edge
solve_edge (tiles_per_edge * (tiles_per_edge - 1), 1)  # solve right edge

solve_corner (tiles_per_edge^2, 1, true)  # bottom right corner

for y in 1 -> tiles_per_edge - 1 do
{
	for x in 1 -> tiles_per_edge - 1 do
	{
		solve_center (y * tiles_per_edge + x)
	}
}

def across (grids)
{
	let line_arrays = grids map { v.lines() }
	
	let edge = line_arrays[ 0 ][ 0 ].length
	
	def make_row (i)
	{
		return (line_arrays map { v[ i ] } per str)
	}
	
	return 0 -> edge map make_row map { v "\n" } per string
}

def trim_transformed (grid, transform)
{
	let transformed_grid = grid_transforms[ transform ] grid
	
	let lines = transformed_grid.lines()
	
	let trimmed = 1 -> edge - 1
	
	return "\n".join( *(lines[ trimmed ] map { v[ trimmed ] }), "")
}

display ""

let trimmed = tile_grid map { trim_transformed (tileset[ v.key ], v.value) }

var final_grid = ""

for Y in 0 -> tiles_per_edge do
{
	let i = Y * tiles_per_edge
	let j = i + tiles_per_edge
	
	final_grid .= across (trimmed[i -> j])
}

let grid_lines = final_grid.lines()

let grid_height = grid_lines.length
let grid_width  = grid_lines[ 0 ].length
let grid_stride = grid_width + 1

let sea_monster = DATA.lines()

let sm_height = sea_monster.length
let sm_width  = sea_monster[ 0 ].length

let gutter = "\0" * (grid_stride - sm_width)

# We H-flip the sea monster
let sm_mask = packed gutter.join *(sea_monster map {-v})

var count = 0

def search_grid (grid)
{
	var p = begin grid
	
	while p = p.find "###" do
	{
		var i = p.past.length
		
		let x = i mod grid_stride
		let y = i div grid_stride
		
		if y in 1 -> grid_height - 1 and x + sm_width <= grid_width then
		{
			i -= grid_stride
			
			let substring = packed grid[ i -> i + sm_mask.size ]
			
			if substring | sm_mask == substring then
			{
				++count
			}
		}
		
		++p
	}
}

for x in grid_transforms do
{
	search_grid x final_grid
}

def count_hash (s)
{
	return Iter.popcount translated (s, " .\n", "\0" * 3)
}

let roughness = count_hash final_grid
              - count_hash DATA * count

display ""

put roughness

end.
                  # 
#    ##    ##    ###
 #  #  #  #  #  #   
