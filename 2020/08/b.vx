#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let n = data.length

def parse (s)
{
	let (opcode, operand) = s / ' '
	
	return [opcode, int operand]
}

def visited_set (program)
{
	var seen = x"00" * n
	
	var PC = 0
	
	while PC < n and not seen[ PC ] do
	{
		let op = program[ PC ]
		
		seen[ PC ] = '\xFF'
		
		++PC
		
		if op[0] == "jmp" then
		{
			PC += op[ 1 ] - 1
		}
	}
	
	return seen
}

def accumulation (program)
{
	var seen = x"00" * n
	
	var ACC = 0
	var PC = 0
	
	while PC < n and not seen[ PC ] do
	{
		let op = program[ PC ]
		
		seen[ PC ] = '\xFF'
		
		++PC
		
		if op[0] == "jmp" then
		{
			PC += op[ 1 ] - 1
		}
		else if op[0] == "acc" then
		{
			ACC += op[1]
		}
	}
	
	return [ACC] (*) (PC >= n)
}

let program = data map parse

let seen = visited_set program

let candidates = 0 -> n ver { seen[ v ] and program[ v ][ 1 ] != 1 }

let jmps = candidates ver { program[ v ][ 0 ] == "jmp" }
let nops = candidates ver { program[ v ][ 0 ] == "nop" }

let jmp_fixes = jmps map { var x = program; x[ v ][ 0 ] = "nop"; x }
let nop_fixes = nops map { var x = program; x[ v ][ 0 ] = "jmp"; x }

let fixed_programs = [*jmp_fixes, *nop_fixes]

for fixed in fixed_programs do
{
	if let acc = accumulation fixed then
	{
		put *acc
		
		exit 0
	}
}
