#!/usr/bin/env minivx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path)

const lines = data.lines()
const width = lines[ 0 ].length
const stride = width + 1

const blank = "~" * stride
const grid = str (blank, lines map { v "~" }, blank)

const digit = '0' .. '9'

def neighbors (index)
{
	return
	[
		index - 1, index - stride,
		index + 1, index + stride,
	]
}

def is_low (i)
{
	const height = grid[ i ]
	
	return height in digit and not (neighbors i ver { grid[ v ] <= height })
}

def risk (i)
{
	return 1 + int str grid[ i ]
}

const sum = stride -> grid.length - stride ver is_low map risk per Math.sum
print sum
