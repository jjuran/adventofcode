#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let links = (load input_path).lines() map { [v / '-'] }

var graph = str^[]

for cave in (links map { *v }) do
{
	graph[ cave ] = []  # sequence has duplicates, but this is idempotent
}

for link in links do
{
	let (a, b) = *link
	
	graph[ a ] <-- b
	graph[ b ] <-- a
}

for adjacent in graph[ "start" ] do
{
	graph[ adjacent ] = graph[ adjacent ] ver { v != "start" }
}

def paths_from (path)
{
	if path[ 0 ] == "end" then
	{
		return 1
	}
	
	var n = 0
	
	for dst in graph[ path[ 0 ] ] do
	{
		if not (dst[ 0 ] in 'a' .. 'z' and dst in path) then
		{
			n += paths_from [dst, *path]
		}
		else if not "" in path then
		{
			n += paths_from [dst, "", *path]
		}
	}
	
	return n
}

put paths_from ["start"]
