#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = [(load input_path).lines()[ 0 ] / ','] map int

let n = data.length

let sorted = Iter.sorted data

def burn (dist)
{
	return dist * (dist + 1) div 2
}

def cost (median)
{
	return data map { v - median } map abs map burn per Math.sum
}

let min_x = sorted[ 0 ]
let max_x = sorted[ n - 1 ]

var minimum = cost min_x

for i in min_x + 1 .. max_x do
{
	let fuel = cost i
	
	if fuel > minimum then
	{
		break
	}
	
	minimum = fuel
}

put minimum
