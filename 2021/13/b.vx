#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

def parse
{
	let (dots_chunk, folds_chunk) = load input_path / "\n\n"
	
	let dots = [dots_chunk / '\n'] map { [eval v] }
	
	def parse_fold (line)
	{
		let (axis, pos) = substr (line, "fold along ".length) / '='
		
		return [u8 *axis - u8 'x', int pos]
	}
	
	let folds = folds_chunk.lines() map parse_fold
	
	return dots, folds
}

def folded (axis, fold, dot)
{
	if dot[ axis ] > fold then
	{
		dot[ axis ] = fold - (dot[ axis ] - fold)
	}
	
	return dot
}

var dots, let folds = parse()

for fold in folds do
{
	dots = dots map folded % *fold
}

let max_x = Iter.max (dots map { v[ 0 ] })
let max_y = Iter.max (dots map { v[ 1 ] })

let width  = max_x + 1
let height = max_y + 1
let stride = width + 1

var grid = (" " * width) "\n" * height

for dot in dots do
{
	let (x, y) = *dot
	
	grid[ x + y * stride ] = '#'
}

put grid
