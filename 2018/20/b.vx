#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vsl" }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = *(load input_path).lines()

var grid = "\n".join ("#+#", "+X+", "#+#", "")

var width = 3
var height = 3
var stride = width + 1

var X = 1
var Y = 1

let dirs = byte^
[
	'N': [0, -1],
	'S': [0,  1],
	'W': [-1, 0],
	'E': [ 1, 0],
]

def route (p, dx=0, dy=0)
{
	display (100 * p.past.length div (p.past.length + p.rest.length)) "%"
	
	var x = X + dx
	var y = Y + dy
	
	var c
	
	while not (c = *p++) in "|)$" do
	{
		if c in dirs then
		{
			let (dir_x, dir_y) = *dirs[ c ]
			
			x += dir_x
			y += dir_y
			
			grid[ x + y * stride ] = "-|"[ y % 2 ]
			
			x += dir_x
			y += dir_y
			
			if not y in 0 -> height then
			{
				let width2 = width div 2
				
				let walls = ("#+" * width2) "#\n"
				let floor = ("+." * width2) "+\n"
				
				if y < 0 then
				{
					grid = walls floor grid
					
					Y += 2
					y += 2
				}
				else
				{
					grid .= floor walls
				}
				
				height += 2
			}
			else if not x in 0 -> width then
			{
				let lines = grid.lines()
				
				let expand =
				if x < 0 then
				{
					X += 2
					x += 2
					
					def expand (v) { ["#+", "+."][ v % 2 ] lines[ v ] '\n' },
				}
				else
				{
					def expand (v) { lines[ v ] ["+#", ".+"][ v % 2 ] '\n' },
				}
				
				grid = 0 -> lines.length map expand per str
				
				width  += 2
				stride += 2
			}
		}
		else if c == '(' then
		{
			do
			{
				dx = x - X
				dy = y - Y
				
				p = route (p, dx, dy)
				
				x = X + dx
				y = Y + dy
				
				c = *p++
			}
			while c == '|'
		}
	}
	
	return --p
}

route (begin data + '^')

grid = translated (grid, "+", "\0")

let offsets =
[
	-1, -stride,
	 1,  stride,
]

def thousanders (start)
{
	var n_thousanders = 0
	
	var distance = 0
	
	var dmap = vector[ u16 ] (height * stride)
	
	var frontier = [ start ]
	
	while frontier do
	{
		var new_frontier = []
		
		for i in frontier do
		{
			if not dmap[ i ] then
			{
				dmap[ i ] = distance
				
				n_thousanders += +(distance >= 1000)
				
				for cell in (offsets ver { grid[ i + v ] } map { i + v * 2 }) do
				{
					new_frontier <-- cell
				}
			}
		}
		
		++distance
		
		frontier <- new_frontier
	}
	
	return n_thousanders
}

display ""

put thousanders (X + Y * stride)
