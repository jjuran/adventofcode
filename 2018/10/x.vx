#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

let space = ' '*
let digit = '0' .. '9'
let coord = ['-'?, digit+] => int
let tuple = ['<', space, coord, [',', space, coord]*, '>']
let datum = ["position=", tuple, " velocity=", tuple] => { [_] }

def parse (s)
{
	var p = begin s
	
	return p += datum
}

def project (t, particle)
{
	var (x, y), let (dx, dy) = *particle
	
	x += dx * t
	y += dy * t
	
	return [x, y]
}

def bounding_box (particles)
{
	let min_x = particles map { v[ 0 ] } per Math.min
	let min_y = particles map { v[ 1 ] } per Math.min
	
	let max_x = particles map { v[ 0 ] } per Math.max
	let max_y = particles map { v[ 1 ] } per Math.max
	
	return [min_x, min_y, max_x, max_y]
}

def center_of_gravity (particles)
{
	let x = (particles map { v[ 0 ] } per Math.sum) div particles.length
	let y = (particles map { v[ 1 ] } per Math.sum) div particles.length
	
	return [x, y]
}

def mass_vector (particles)
{
	let dx = particles map { v[ 2 ] } per Math.sum
	let dy = particles map { v[ 3 ] } per Math.sum
	
	return [dx, dy]
}

let particles = data map parse

def clustering_shift (t)
{
	let box0 = bounding_box (particles map project %   t)
	let box1 = bounding_box (particles map project % ++t)
	
	let w0 = box0[ 2 ] - box0[ 0 ]
	let w1 = box1[ 2 ] - box1[ 0 ]
	let h0 = box0[ 3 ] - box0[ 1 ]
	let h1 = box1[ 3 ] - box1[ 1 ]
	
	let shifts =
	[
		w1 <=> w0,
		h1 <=> h0,
	]
	
	if Math.max *shifts == Math.min *shifts then
	{
		return shifts[ 0 ]
	}
	
	return 0
}

let mvec = mass_vector particles

var T = 0

var min_t = 0

while clustering_shift T < 0 do
{
	min_t = T
	
	display "T = " T
	
	let points = particles map project % T
	
	let cog = center_of_gravity points
	
	let dt_x = cog[ 0 ] * points.length div -mvec[ 0 ]
	let dt_y = cog[ 1 ] * points.length div -mvec[ 1 ]
	
	let dt = Math.min (abs dt_x, abs dt_y) div 2
	
	if dt == 0 then {break}
	
	T += dt
}

var inc = min_t

while inc > 0 do
{
	T = min_t
	
	while clustering_shift T < 0 do
	{
		min_t = T
		
		display "T = " T
		
		T += inc
	}
	
	inc = inc div 2
}

display ""

put T

let points = particles map project % T

let min_x = Iter.min (points map {v[ 0 ]})
let max_x = Iter.max (points map {v[ 0 ]})
let min_y = Iter.min (points map {v[ 1 ]})
let max_y = Iter.max (points map {v[ 1 ]})

assert min_x >= 0
assert min_y >= 0

let width  = max_x + 1 - min_x
let height = max_y + 1 - min_y

let npx = width * height

var grid = " " * npx

for point in points do
{
	var (x, y) = *point
	
	x -= min_x
	y -= min_y
	
	grid[ x + width * y ] = '*'
}

put "\n    ".join( "", grid / height, "" )
