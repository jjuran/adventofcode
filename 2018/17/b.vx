#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]
let libdir = bindir "/../../lib/"

def import (lib) { eval (load, realpath) libdir lib ".vy" }

let display = (import "status") { OUT <== _ }

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

def rangify (s)
{
	let v = s / ".."
	
	return [v (*) 3 - (+) v] map int
}

def coordinate (s)
{
	let (symbol, data) = s / '='
	
	return *symbol => rangify data
}

def parse (line)
{
	let coordinates = [line / ", "] map coordinate
	
	return Iter.sorted coordinates via .key map .value
}

let coordinates = data map parse

let x_coords = coordinates map { v[ 0 ] }
let y_coords = coordinates map { v[ 1 ] }

let min_x = (x_coords map { v[ 0 ] } per Math.min) - 1
let max_x = (x_coords map { v[ 1 ] } per Math.max) + 1
let min_y = (y_coords map { v[ 0 ] } per Math.min)
let max_y = (y_coords map { v[ 1 ] } per Math.max)

let width  = max_x + 1 - min_x
let height = max_y + 1
let stride = width + 1

var grid = (("." * width) "\n" * height) ("_" * width) "\n"

let spring = 500 - min_x

grid[ spring ] = '+'

for vein in coordinates do
{
	let (xs, ys) = *vein
	
	let (x0, x1) = *xs
	let (y0, y1) = *ys
	
	for y in y0 .. y1 do
	{
		for x in x0 - min_x .. x1 - min_x do
		{
			grid[ x + y * stride ] = '#'
		}
	}
}

def bounded (i)
{
	var j = i
	
	while grid[ --i ] == '|' and grid[ i + stride ] in "~#" do {};
	while grid[ ++j ] == '|' and grid[ j + stride ] in "~#" do {}
	
	if grid[ i ] == '#' and grid[ j ] == '#' then
	{
		return [i + 1, j - 1]
	}
	
	return []
}

def spread (source)
{
	let below = source + stride
	
	if grid[ below ] == '.' then
	{
		grid[ below ] = '|'
		
		return [below]
	}
	
	if grid[ below ] in "~#" then
	{
		let adjacent = [1, -1] map { source + v } ver { grid[ v ] == '.' }
		
		for i in adjacent do
		{
			grid[ i ] = '|'
		}
		
		if let bounds = bounded source then
		{
			let (left, right) = *bounds
			
			for i in left .. right do
			{
				grid[ i ] = '~'
			}
			
			return left .. right map { v - stride } ver { grid[ v ] == '|' }
		}
		
		return adjacent
	}
	
	return []
}

var T = 0
var next = [spring]

do
{
	display "T = " ++T
}
while next = next map spread map { *v }

display ""

let proper_grid = substr (grid, min_y * stride)

put Iter.popcount translated (proper_grid, ".#|_+\n", "\0" * 6)
