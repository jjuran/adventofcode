#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

var grid = load input_path

const grid_len = grid.length

const width = (begin grid).find('\n').past.length
const stride = width + 1
const height = grid_len / stride

const E = 0
const S = 1
const W = 2
const N = 3
const X = 4

const deltas =
[
	1,
	stride,
	-1,
	-stride,
]

const arrows = ">v<^X"

var carts = []

carts <-- *(0 -> grid_len ver { grid[ v ] == '^' } map { v => N => 1 })
carts <-- *(0 -> grid_len ver { grid[ v ] == 'v' } map { v => S => 1 })
carts <-- *(0 -> grid_len ver { grid[ v ] == '<' } map { v => W => 1 })
carts <-- *(0 -> grid_len ver { grid[ v ] == '>' } map { v => E => 1 })

const background = translated (grid, "^v<>", "||--")

var crashed = ()

def move_cart (cart)
{
	var address = cart.key
	var heading = cart.value.key
	var chooser = cart.value.value
	
	grid[ address ] = background[ address ]
	
	address += deltas[ heading ]
	
	const c = grid[ address ]
	
	if c in "^v<>" then
	{
		heading = X
		crashed = address mod stride, address div stride
	}
	else if c == '/' then
	{
		# N (3) <-> E (0)
		# S (1) <-> W (2)
		heading = 3 - heading
	}
	else if c == '\\' then
	{
		# N (3) <-> W (2)
		# S (1) <-> E (0)
		heading = heading + 1 - heading % 2 * 2
	}
	else if c == '+' then
	{
		heading = (heading - chooser) mod 4
		
		chooser = chooser mod 3 - 1
	}
	
	grid[ address ] = arrows[ heading ]
	
	return address => heading => chooser
}

def move_carts
{
	carts = Iter.sorted carts via .key
	
	for i in 0 -> carts.length do
	{
		carts[ i ] = move_cart carts[ i ]
		
		if crashed then
		{
			break
		}
	}
}

while not crashed do
{
	move_carts()
}

const crash_site = ",".join crashed
print crash_site
