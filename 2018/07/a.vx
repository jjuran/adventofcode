#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

var prereqs = byte^[]

for row0 in data do
{
	let a = row0[  5 ]
	let b = row0[ 36 ]
	
	prereqs[ a ] =
	prereqs[ b ] = []
}

for row in data do
{
	let a = row[  5 ]
	let b = row[ 36 ]
	
	prereqs[ b ] <-- a
}

def next
{
	return prereqs ver {v.value.length == 0} map .key per Math.min
}

var order = []

while prereqs do
{
	let step = next()
	
	order <-- step
	
	prereqs = prereqs ver {v.key != step} map {v.key => v.value ver {v != step}}
}

put str order
