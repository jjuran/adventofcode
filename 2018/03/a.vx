#!/usr/bin/env vx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path).lines()

var fabric = vector[u8, 1000^2]()

def rect_indices (x, y, w, h)
{
	return y -> y + h map { const i = v * 1000 + x; *(i -> i + w) }
}

for row in data do
{
	const p = (begin row).find '@' + 2
	
	const claim = p.rest
	
	var q = (begin claim).find ':'
	
	const offset = q.past
	
	q += 2
	
	const area = q.rest
	
	const (x, y) = *([offset / ','] map int)
	const (w, h) = *([area   / 'x'] map int)
	
	const indices = rect_indices( x, y, w, h )
	
	for i in indices do
	{
		var v = u8 (fabric[ i ] + 1)
		
		if v then
		{
			fabric[ i ] = v
		}
	}
}

const overlapped = [*fabric] ver {v > 1}

print overlapped.length
