#!/usr/bin/env lvx

let bindir = dirname realpath argv[ 0 ]

let input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

let data = (load input_path).lines()

var fabric = vector[u8, 1000^2]()

def rect_indices (x, y, w, h)
{
	return y -> y + h map { let i = v * 1000 + x; *(i -> i + w) }
}

for row in data do
{
	let p = (begin row).find '@' + 2
	
	let claim = p.rest
	
	var q = (begin claim).find ':'
	
	let offset = q.past
	
	q += 2
	
	let area = q.rest
	
	let (x, y) = *([offset / ','] map int)
	let (w, h) = *([area   / 'x'] map int)
	
	let indices = rect_indices( x, y, w, h )
	
	for i in indices do
	{
		var v = u8 (fabric[ i ] + 1)
		
		if v then
		{
			fabric[ i ] = v
		}
	}
}

var id

for row2 in data do
{
	let p = (begin row2).find '@' + 2
	
	let claim = p.rest
	
	var q = (begin claim).find ':'
	
	let offset = q.past
	
	q += 2
	
	let area = q.rest
	
	let (x, y) = *([offset / ','] map int)
	let (w, h) = *([area   / 'x'] map int)
	
	let indices = rect_indices( x, y, w, h )
	
	id = (p + -3).past
	
	for i in indices do
	{
		if fabric[ i ] > 1 then
		{
			id = null
			break
		}
	}
	
	if id then {break}
}

put substr( id, 1 )
