#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]

const input_path = if argc > 1 then {argv[ 1 ]} else {bindir "/input.txt"}

const data = (load input_path).lines()

const n_lines = data.length

const IP_reg = int substr (data[ 0 ], "#ip ".length)

const program = data[ 1 -> n_lines ]

const n_instructions = n_lines - 1

def AND (a, b)
{
	return u8 *(packed byte a & packed byte b)
}

def OR (a, b)
{
	return u8 *(packed byte a | packed byte b)
}

def GT (a, b)
{
	return u8 (a > b)
}

def EQ (a, b)
{
	return u8 (a == b)
}

const imm = ...

var registers = [ 0 (*) 6 ]

def reg (i) { registers[ i ] }

const instruction_set = str^
[
	addr: [ reg, reg, Math.sum ],
	addi: [ reg, imm, Math.sum ],
	mulr: [ reg, reg, Math.product ],
	muli: [ reg, imm, Math.product ],
	banr: [ reg, reg, AND ],
	bani: [ reg, imm, AND ],
	borr: [ reg, reg, OR ],
	bori: [ reg, imm, OR ],
	setr: [ reg, ..., head ],
	seti: [ imm, ..., head ],
	gtrr: [ reg, reg, GT ],
	gtri: [ reg, imm, GT ],
	gtir: [ imm, reg, GT ],
	eqrr: [ reg, reg, EQ ],
	eqri: [ reg, imm, EQ ],
	eqir: [ imm, reg, EQ ],
]

def almost_sqrt (x)
{
	var min = 1
	
	while min^2 < x do
	{
		++min
	}
	
	return min - 1
}

registers[ 0 ] = 1

var IP = 0
var n = 0

while ++n <= n_instructions and IP < n_instructions do
{
	registers[ IP_reg ] = IP
	
	const line = program[ IP ]
	
	const mnemonic = line[ 0 -> 4 ]
	const operands = [substr (line, 5) / ' '] map int
	
	const (mode1, mode2, f) = *instruction_set[ mnemonic ]
	
	const (a, b, c) = *operands
	
	registers[ c ] = f (mode1 a, mode2 b)
	
	IP = registers[ IP_reg ] + 1
}

const x = Iter.max registers

const x2 = almost_sqrt x

var factor_sum = 1 .. x2 ver { x % v == 0 } map { v + x / v } per Math.sum

factor_sum += x2 * +(x2^2 == x)

print factor_sum
