#!/usr/bin/env lvx

const bindir = dirname realpath argv[ 0 ]
const libdir = bindir "/../../lib/"

const import = { const path = libdir _ ".vsl"; eval( load path, path ) }

const display = (import "status") { OUT <== _ }

const data = if argc > 1 then {argv[ 1 ]} else {"147061"}

const n = data.length

var scoreboard = "37"

var i = 0
var j = 1
var N = 0

while true do
{
	const one = int scoreboard[[ i ]]
	const two = int scoreboard[[ j ]]
	
	scoreboard .= str (one + two)
	
	N = scoreboard.length - n
	
	if substr (scoreboard, N - 1, n) == data then
	{
		--N
		break
	}
	
	if substr (scoreboard, N, n) == data then
	{
		break
	}
	
	if N mod 2^12 == 0 then
	{
		display "" (N div 2^12)
	}
	
	i = (i + 1 + one) mod scoreboard.length
	j = (j + 1 + two) mod scoreboard.length
}

display ""

print N
